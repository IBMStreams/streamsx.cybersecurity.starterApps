namespace com.ibm.streamsx.cybersecurity.sample ;

use com.ibm.streams.cybersecurity.tagging::BWListTagger ;
use com.ibm.streams.cybersecurity.types::* ;
use com.ibm.streamsx.network.ipv4::* ;

public composite BWListTaggerIPv4NumericList
{
	graph
		(stream<list<rstring> ips, rstring extra> SrcStream) as
			SrcOp = FileSource()
		{
			param
				file : getThisToolkitDir() + "/opt/input_ipv4_vec.txt" ;
			output
				SrcStream : extra = "abcdef" ;
		}

		(stream<list<IPv4> ips, rstring extra> IPStrToNumStream) as IPStrToNumOp =
			Functor(SrcStream)
		{
			output
				IPStrToNumStream : ips = convertIPV4AddressStringsToNumerics(ips) ;
		}

		(stream<list<IPv4> ips, rstring extra, list<BWListTag_e> ipTags>
			TaggedStream) as BWTaggerOp = BWListTagger(IPStrToNumStream
			as inPort0Alias)
		{
			param
				ipAttr : ips ;
				blockIPFile : getThisToolkitDir() + "/opt/bwlist/IPBlockList.txt" ;
				allowIPFile : getThisToolkitDir() + "/opt/bwlist/IPAllowList.txt" ;
				blockDomainFile : getThisToolkitDir() + "/opt/bwlist/DomainsBlockList.txt" ;
				allowDomainFile : getThisToolkitDir() + "/opt/bwlist/DomainsAllowList.txt" ;
			output
				TaggedStream : ipTags = getIPTags() ;
		}

		() as Printer = Custom(IPNumToStrStream as inPort0Alias)
		{
			logic
				onTuple inPort0Alias : println(inPort0Alias) ;
		}

		(stream<list<rstring> ips, rstring extra, list<BWListTag_e> ipTags>
			IPNumToStrStream) as IPNumToStrOp = Functor(TaggedStream)
		{
			output
				IPNumToStrStream : ips = convertIPV4AddressNumericsToStrings(ips) ;
		}

}
